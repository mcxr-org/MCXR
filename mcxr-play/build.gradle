plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower-mini' version '1.1.0'
    id 'maven-publish'
    //id 'org.ajoberstar.grgit' version '5.0.0-rc.3'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "mcxr-play"
version = "${project.play_version}+${rootProject.getVersionMetadata(this)}"
group = project.maven_group

repositories {
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

dependencies {
    implementation project(path: ":mcxr-core", configuration: "namedElements")

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modCompileOnly("com.github.Virtuoel:Pehkui:${project.pehkui_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    implementation("org.joml:joml:${project.joml_version}")
    include(modImplementation("com.github.Sorenon:fart:51f6a721e7"))

    implementation group: 'org.lwjgl', name: 'lwjgl', version: '3.2.3'
    implementation group: 'org.lwjgl', name: 'lwjgl-glfw', version: '3.2.3'
    implementation group: 'org.lwjgl', name: 'lwjgl-jemalloc', version: '3.2.3'
    implementation group: 'org.lwjgl', name: 'lwjgl-openal', version: '3.2.3'
    implementation group: 'org.lwjgl', name: 'lwjgl-opengl', version: '3.2.3'
    implementation group: 'org.lwjgl', name: 'lwjgl-stb', version: '3.2.3'
    implementation(group: 'org.lwjgl', name: 'lwjgl-tinyfd', version: '3.2.3')
}

sourceSets {
    main {
        resources {
            srcDir 'loader'
        }
    }
}

processResources {
    inputs.property "play_version", project.play_version
    inputs.property "core_version", project.core_version

    filesMatching("fabric.mod.json") {
        expand "play_version": project.play_version, "core_version": project.core_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

loom {
    runs {
        playClient {
            client()
            configName = "MCXR Play Client"
            ideConfigGenerated(true)
        }
    }
}
